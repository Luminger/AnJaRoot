apply plugin: 'android'

android {
    compileSdkVersion 19
    buildToolsVersion '19.1.0'
    defaultConfig {
        applicationId 'org.failedprojects.anjaroot'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    signingConfigs {
        release {
            storeFile file('/home/simon/workspace/android.jks')
            keyAlias 'main'
            storePassword "dummy"
            keyPassword "dummy"
        }
    }
    buildTypes {
        release {
            runProguard false
            debuggable false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            zipAlign true
        }
        debug {
            debuggable true
            jniDebugBuild true
        }
    }
    sourceSets.main {
        jni.srcDirs = [] // prevent gradle from running ndk-build on its own
    }
    productFlavors {
    }
}

task('readPasswordFromInput') << {
    def console = System.console()

    def password = new String(console.readPassword('\n\n\n> Enter keystore password: '))

    android.signingConfigs.release.storePassword = password
    android.signingConfigs.release.keyPassword = password
}

tasks.whenTaskAdded { task ->
    if (task.name.matches("validateReleaseSigning")) {
        task.dependsOn('readPasswordFromInput')
    }
}

task buildBuiltinZip(type: Exec) {
    commandLine 'make', '-C', file('src/main').absolutePath, 'apkrelease'
}

task buildZip(type: Exec) {
    commandLine 'make', '-C', file('src/main').absolutePath, 'release'
}

task cleanZip(type: Exec) {
    commandLine 'make', '-C', file('src/main').absolutePath, 'clean'
}

tasks.withType(JavaCompile) { compileTask -> compileTask.dependsOn buildBuiltinZip }

clean.dependsOn 'cleanZip'

assembleRelease << {
    buildZip.execute()
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:19.+'
    compile project(':library')
}
